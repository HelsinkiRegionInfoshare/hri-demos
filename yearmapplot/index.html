<!doctype html>
<html>
<meta charset="utf-8" />
<head>
<title></title>

<!-- IE hacks -->
<script type="text/javascript" src="../vendor/es5-shim.min.js"></script>
<script type="text/javascript" src="../vendor/excanvas.min.js"></script>
<!--[if IE]>
<script type="text/javascript" src="../vendor/tinycolor-min.js"></script>
<script type="text/javascript">
window.mangle_hsl_color = function (hsl) {
    return tinycolor(hsl).toRgbString()
}
</script>
<![endif]-->

<script type="text/javascript" src="../vendor/jquery-1.11.0.js"></script>
<script type="text/javascript" src="../vendor/notify.min.js"></script>
<script type="text/javascript" src="../vendor/coffee-script.js"></script>
<script type="text/javascript" src="../vendor/underscore.js"></script>
<script type="text/javascript" src="../vendor/bootstrap/js/bootstrap.js"></script>

<script type="text/javascript" src="../vendor/chosen/chosen.jquery.js"></script>
<link rel="stylesheet" href="../vendor/chosen/chosen.css" />
<script type="text/javascript" src="../vendor/bootstrap-multiselect.js"></script>

<script type="text/javascript" src="../vendor/jquery.flot.js"></script>
<script type="text/javascript" src="../vendor/jquery.flot.resize.js"></script>
<script type="text/javascript" src="../vendor/jquery-deparam.js"></script>
<script type="text/javascript" src="../vendor/jquery-ui/jquery-ui.min.js"></script>
<link rel="stylesheet" href="../vendor/jquery-ui/jquery-ui.css" />
<script type="text/javascript" src="../vendor/slider/js/bootstrap-slider.js"></script>
<link rel="stylesheet" href="../vendor/slider/css/slider.css" />
<script type="text/javascript" src="../vendor/spin.js"></script>
<script type="text/javascript" src="../vendor/jquery.spin.js"></script>
<link rel="stylesheet" href="../vendor/jquery.powertip/jquery.powertip.css" />
<script type="text/javascript" src="../vendor/jquery.powertip/jquery.powertip.js"></script>

<link rel="stylesheet" href="../vendor/leaflet.css" />
<script src="../vendor/leaflet.js"></script>
<script src="../vendor/proj4.js"></script>
<script src="../vendor/proj4leaflet.js"></script>
<script src="../vendor/leaflet-dvf.js"></script>
<script type="text/coffeescript" src="hel_map.coffee"></script>

<!-- Include the Statproxy class -->
<script type="text/coffeescript" src="../vendor/statproxy.coffee"></script>

<link href="../vendor/bootstrap/css/bootstrap.min.css" rel="stylesheet">
<style type="text/css">
body, html {
	width: 100%;
	height: 100%;
	margin: 0;
}

#map {
	height: 100%;
	width: 100%;
	background-color: white;
	position: absolute;
	top: 0;
	left: 0;
	bottom: 0;
	right: 0;
	height: auto;
	width: auto;
}

#map .leaflet-control-scale {
	display: none;
}

#controls {
	position: absolute;
	top: 0;
	left: 0;
	width: 100%;
	z-index: 1;
}

#controls > * {
	background-color: rgba(255,255,255,0.9);
	padding: 10px;
}


#lineplot {
	position: absolute;
	display: table;
	background-color: rgba(255,255,255,0.9);
	bottom: 10px;
	left: 10px;
	padding: 5px;
	width: 500px;
	height: 300px;
	cursor: move;
}

#lineplot > div {
	display: table-row;
}

#lineplot .plot_container {
	height: 100%;
	width: 100%;
}

.variable_label {
	display: block;
	font-weight: bold;
}

.plot_x_label,
.plot_y_label {
	font-size: small;
	font-weight: bold;
	text-align: center;
	padding: 0;
	margin: 0;
}

.variable_selector .chosen-container {
	width: 100% !important;
}

.variable_selector .chosen-container-multi .chosen-choices {
	overflow-x: auto;
	overflow-y: hidden;
	white-space: nowrap;
}

.variable_selector .chosen-container-multi .chosen-choices li {
	display: inline-block;
	float: none;
}

#map_colormap_legend {
	position: absolute;
	width: 300px;
	padding: 10px;
	bottom: 10px;
	right: 50px;
	background-color: rgba(255,255,255,0.9);

}

</style>

</head>
<body>

<div id="controls" class="container-fluid">
  <div class="row" id="variable_selectors">
	<h4 class="col-md-3" id="dataset_title"></h4>
	<div class="col-md-3 variable_selector" id="yearslider_container"></div>
  </div>
</div>

<div id="map">
</div>
<div id="map_colormap_legend"></div>
<div id="lineplot"></div>




<script type="text/coffeescript">
# URL to the JSON-"entrypoint" of the statistic.
# Any resource with a 'vuosi' field could be used here
STAT_RESOURCE="http://dev.hel.fi/stats/resources/aluesarjat_a01hki_asuntokuntien_tulot/"
GEOSERVER_BASE_URL = "http://geoserver.hel.fi/geoserver/"

# Get a "proxy object" to the resource
stats = new Statproxy STAT_RESOURCE

# Get the "specification" of the data. Includes
# metadata such as the title and dimensions/categories
# in the dataset.
spec_promise = stats.specification()


# Fetch the area polygons in GeoJSON format using WFS
wfs_url = GEOSERVER_BASE_URL + "wfs/"
request_parameters = {
	# Specify the dataset to use
	typeName: 'hel:osaalue'
	# Standard WFS request parameters
	srsName: 'EPSG:4326'
	service: 'WFS'
	version: '1.1.0'
	request: 'GetFeature'
	outputFormat: 'application/json'
}

request_options = {}

# This would work with IE8, but everything else is a mess
# (it eg. runs out of memory), so give up on non-CORS browsers
# for now.
###
# Use JSONP for browsers that don't support
# CORS
if not $.support.cors
	request_parameters.outputFormat = 'text/javascript'
	request_options = {
		dataType: "jsonp"
		jsonp: true
		# Geoserver has nonstandard JSONP conventions, so
		# we'll override the callback manually
		jsonpCallback: "parseResponse"
	}
###

request_options.data = request_parameters
polygons_promise = $.ajax wfs_url, request_options

class SelectionController
	constructor: (@specification) ->
		@selection = get_url_filtering()
		@$ = $ @
	
	_do_set_selection: (new_selection) ->
		# Update the selection to the url
		@selection = new_selection
		update_url_filtering @selection
		@trigger_change()
	
	_selection_clone: ->
		JSON.parse JSON.stringify @selection

	update_selection: (dimension, category, enabled) =>
		selection = @_selection_clone()
		if dimension not of selection
			selection[dimension] = {}
		
		if not enabled
			delete selection[dimension][category]
		else
			selection[dimension][category] = enabled
		@_do_set_selection selection

		
	
	set_selection: (dimension, categories) =>
		if not _.isArray categories
			categories = [categories]
		
		selection = @_selection_clone()
		selection[dimension] = {}
		for category in categories
			selection[dimension][category] = true

		@_do_set_selection selection
	
	toggle: (dimension, category) =>
		selection = @_selection_clone()
		if dimension not of selection
			selection[dimension] = {}
		if category of selection[dimension]
			delete selection[dimension][category]
		else
			selection[dimension][category] = true

		@_do_set_selection selection
	
	
	trigger_change: =>
		@$.trigger 'selection_changed', [@selection, @has_full_selection]
		
	
	has_full_selection: (axes) =>
		# Check if we have at least one selection for
		# each dimension.
		for dim in @specification.dimensions
			dim_id = dim.id
			if dim_id in axes
				continue

			if dim_id not of @selection
				return false
			if _.isEmpty @selection[dim_id]
				return false
		return true

class PlotController
	constructor: (@el, @stats, @specification, @axes) ->
		@$el = $ @el
		
		# Use only horizontal labels, as they can be quite long
		# and don't wrap nicely when rotated.
		@$el.append $ """
		<h3 class="plot_y_label"></h3>
		<div class="plot_container">
			<div class="plot_actual_container" style="height: 100%;"></div>
		</div>
		<h4 class="plot_x_label"></h4>
		"""
		@plot_el = @$el.find(".plot_actual_container")[0]
		@xlabel_el = @$el.find(".plot_x_label")
		@ylabel_el = @$el.find(".plot_y_label")
		
		# The jquery-ui plugins use the top-property, so
		# swapidi swap. Causes a bit weird behavior on window
		# resize, but definitely less weird than without it
		@$el.css
			top: @$el.position().top
			bottom: "auto"

		@$el.draggable().resizable()

		# Convert the specification to an object so it's
		# nicer to work with
		@dictspec = Statproxy.specification_to_object @specification
		
		@_current_selection = null
		
		$(@plot_el).powerTip
			followMouse: true
			manual: true
			closeDelay: 0
			fadeOutTime: 0
			fadeInTime: 0

	
	refresh_data: (selection, is_full) =>
		my_selection = _.omit selection, @axes...
		# Ensure we have at least one selection for
		# each dimension. Failing to do this will cause
		# a massive dataset, as without filtering, the
		# default is to return all results.
		if not is_full @axes
			@_current_selection = {}
			@refresh_plot {}, []
			return

		# No need to refresh if the selection is same
		# than we had already plotted
		if _.isEqual my_selection, @_current_selection
			return

		@_current_selection = my_selection
		@$el.spin()
		

		# First filter for the selected categories
		substats = @stats.filter my_selection

		# The plot needs to know the selection of the data.
		# This may change as the fetching/drawing is still in progress,
		# so we'll have to deep-copy it.
		selection_clone = JSON.parse JSON.stringify my_selection

		# As we allow multiple categories to be selected
		# simultaneously, any category combination will
		# get its own line, so they need to be "grouped".
		# The server (and the proxy)
		# has a "group_for_columns" method for this.
		# Get the needed data and pass it to the plotter
		substats.group_for_columns(@axes).done (data) =>
			@refresh_plot selection_clone, data


	refresh_plot: (selection, data) =>
		# Check which dimensions are included in the label.
		# Use only the dimensions that have more than one
		# option selected.
		needs_labels = []
		y_axis_labels = []
		for dim_id, categories of selection
			if _.size(categories) > 1
				needs_labels.push dim_id
			else if _.size(categories) == 1
				dim = @dictspec.dimensions[dim_id]
				cat = dim.categories[(k for k of categories)[0]]
				y_axis_labels.push cat.label

		
		# Convert the data to a format that the plotting
		# library flot understands.
		datasets = []
		for group in data
			# Convert to numbers
			x = group[@axes[0]].map parseFloat
			y = group[@axes[1]].map parseFloat
			
			# Convert the data to "row-format"
			xy = _.zip x, y
			
			# Generate this group's plot label
			label = []
			for k in needs_labels
				dim = @dictspec.dimensions[k]
				label.push dim.categories[group[k]].label
			datasets.push
				data: xy
				label: label.join ' '
		
		
		@ylabel_el.text y_axis_labels.join ', '
		@xlabel_el.text @dictspec.dimensions[@axes[0]].label
		# And finally plot the datasets using flot
		$.plot @plot_el, datasets,
			series:
				lines: show: true
				points: show: true
			xaxis:
				tickDecimals: 0
			legend:
				position: "nw"
			grid:
				hoverable: true
		
		# (Re-) connect hover events
		$plot_el = $ @plot_el
		$plot_el.off("plothover").on "plothover", (event, pos, item) ->
			if not item
				$plot_el.powerTip "hide"
				# Without this the powertip won't change
				# the contents.
				$.powerTip.hide()
				return
			
			$plot_el.data 'powertipjq', $ """
			<div class="plot_hover_label">
			<h5>#{item.series.label}</h5>
			#{item.datapoint[0]}: #{item.datapoint[1]}
			</div>
			"""
			$plot_el.powerTip "show"

		$(@el).spin(false)

class MapController
	constructor: (@el, @polygons, @stats, @specification, @axes,
			@selection_control, @tooltip_func=((feature) ->)
			) ->
		@$el = $ "#"+el
		@map = hel_leaflet_map @el,
			zoomControl: false
		new L.Control.Zoom({ position: 'bottomright' }).addTo(@map.map)
		
		@map.map.fitBounds [
			[60.14112, 24.8278],
			[60.28228, 25.26318]
			]

		@polygons_by_area = {}
		for polygon in @polygons.features
			# Convert the area_id to integer as the datasets
			# differ in these IDs
			# in whether they are zero-padded or not.
			# TODO: This could be taken as an argument in the
			#	constructor
			area_id = parseInt(polygon.properties.tunnus)
			polygon.properties.area_id = area_id
			@polygons_by_area[area_id] = polygon
		
		@layers_by_area = {}

		@borders_layer = L.geoJson polygons,
			onEachFeature: (feat, layer) =>
				return if 'area_id' not of feat.properties
				@layers_by_area[feat.properties.area_id] = layer

				layer.on click: (e) =>
					feat = e.target.feature
					return if 'raw_area' not of feat.properties
					raw_area = feat.properties.raw_area
					@selection_control.toggle @axes[0], raw_area
			style: ->
				# Show the polygons only after we get data
				return {stroke: false, fill: false}

		@borders_layer.addTo @map.map
		@$el.powerTip
			followMouse: true
			manual: true
			closeDelay: 0
			fadeOutTime: 0
			fadeInTime: 0
		
		@_data_cache = {}
		
	refresh_data: (selection, is_full) =>
		@$el.spin()
		my_selection = _.omit selection, @axes...
		selection_clone = JSON.parse JSON.stringify selection
		my_hashable_selection = JSON.stringify my_selection
		if my_hashable_selection of @_data_cache
			@refresh_plot selection_clone, @_data_cache[my_hashable_selection]
			return

		substats = @stats.filter my_selection
		substats.group_for_columns(@axes).done (data) =>
			@_data_cache[my_hashable_selection] = data
			@refresh_plot selection_clone, data
	
	refresh_plot: (selection, data) =>
		enabled_polygons = []
		data = data[0]
		area_and_value = _.zip data[@axes[0]], data[@axes[1]]
		for [area, value] in area_and_value
			# Parse the "small-area-id" from the area string
			# Encoded as (zero padded) number of three digits
			# in the end. Zero if not 'small area'
			# TODO: This could be taken as an argument in the
			#	constructor
			area_id = area
			area_id = parseInt area_id[area_id.length-3...]
			continue if area_id == 0
			continue if area_id not of @polygons_by_area
			polygon = @polygons_by_area[area_id]
			polygon.properties.stat_value = parseFloat value
			polygon.properties.raw_area = area
			enabled_polygons.push polygon

		# Calculate the range and colormap from it
		all_values = (p.properties.stat_value for p in enabled_polygons \
			when not isNaN p.properties.stat_value)
		
		all_values.sort((a, b) -> a - b)
		min_value = all_values[0]
		max_value = all_values[all_values.length-1]
		# Creates a green (highest) to red (lowest) colormap
		value_to_color = new L.HSLHueFunction(
			new L.Point(min_value,0),
			new L.Point(max_value,120))

		set_colormap_legend($("#map_colormap_legend"), [min_value, max_value],
			(val) -> value_to_color.evaluate(val))

		if @axes[0] of selection
			area_selection = selection[@axes[0]]
		else
			area_selection = {}
		
		styler = (polygon) =>
			# Get the value and force it in to the color mapping range
			value = polygon.properties.stat_value
			if value < min_value
				value = min_value
			if value > max_value
				value = max_value
			# Specify the styling and coloring by value
			color = value_to_color.evaluate(value)
			# IE(8) doesn't support HSL :(
			if window.mangle_hsl_color
				color = mangle_hsl_color(color)
			style =
				stroke: true
				fill: true
				color: "white"
				fillColor: color
				fillOpacity: 0.7
				opacity: 1
				dashArray: '3'
				weight: 1

			if not value
				style.fillOpacity = 0.1
			
	
			layer = @layers_by_area[polygon.properties.area_id]
			
			if area_selection[polygon.properties.raw_area]
				style.weight = 3
				style.color = "black"
				style.dashArray = ''
				layer.bringToFront()
			
			layer.on "mouseover", (e) =>
				tooltip = @tooltip_func e.target.feature
				return if not tooltip
				@$el.data 'powertipjq', tooltip
				@$el.powerTip "show"

			layer.on "mouseout", (e) =>
				@$el.powerTip "hide"
				# Without this the powertip won't change
				# the contents.
				$.powerTip.hide()
			
			return style
		@borders_layer.setStyle styler
		@$el.spin(false)

# Set up the selection UI when we get the data.
# Also hook a body onload promise here, as IE(8)
# won't play ball without.
onload_promise = $.Deferred()
$ -> onload_promise.resolve(null)
initial_data_promise = $.when(spec_promise, polygons_promise, onload_promise)

$("body").spin()

initial_data_promise.done (args...) ->
	$("body").spin(false)
	# Add global error handler on AJAX failures.
	$(document).ajaxError ->
		$.notify """Tietojen lataaminen palvelimelta epäonnistui.""",
	 
	spec = args[0][0]
	polygons = args[1][0]

	selection = new SelectionController spec
	plot = new PlotController "#lineplot", stats, spec, ['vuosi', 'value']

	get_tooltip = (feature) ->
		prop = feature.properties
		return $ """
			<div>
			<h6>#{prop.nimi}</h6>
			#{prop.stat_value}
			</div>
			"""

	map = new MapController("map", polygons, stats, spec, ['alue', 'value'], selection, get_tooltip)
	
	
	# Set the title
	$("#dataset_title").html spec.metadata.label
	document.title = spec.metadata.label

	# Get "free" dimensions, eg on which we have
	# to select something
	free_dims = (dim for dim in spec.dimensions \
		when dim.id not in ['value', 'vuosi'])
	
	multiselect_dims = ['alue']
	
	# Helper function to get a label of a
	# dimension/category if it exists
	get_label = (obj) ->
		if 'label' of obj
			return obj.label
		return obj.id

	# Set up select boxes for the free variables
	selectors = $ "#variable_selectors"
	for dim in free_dims
		container = selectors
		selrow = $("""
			<div class="variable_selector col-md-3">
			  <span class="variable_label">#{get_label dim}</span>
			</div>
			""").appendTo container
		selector = $("""
		<select name="#{dim.id}">
		</select>
		""").appendTo selrow
		is_multiselect = dim.id in multiselect_dims
		
		# "Uniselect" selects first by default
		if not is_multiselect and dim.id not of selection.selection
			selection.set_selection dim.id, dim.categories[0].id
	
		# Synchronize the selection to the dropdowns
		selection.$.on 'selection_changed', do (dim, selector) -> (ev, args...) ->
			selector.find('option').removeAttr "selected"
			if dim.id not of selection.selection
				selector.trigger("chosen:updated")
				return
			for selected of selection.selection[dim.id]
				selector.find("option[value=\"#{selected}\"]").prop "selected", "selected"
			selector.trigger("chosen:updated")

		for cat in dim.categories
			optel = $("""
			<option value="#{cat.id}">
			#{get_label cat}
			</option>
			""").appendTo selector
			
		if is_multiselect
			selector.prop 'multiple', 'multiple'
			selector.change do (dim, selector) -> ->
				selected = $(@).find("option:selected")
				
				cats = ($(s).val() for s in selected)
				selection.set_selection dim.id, cats

		else
			selector.change do (dim, selector) -> ->
				selected = $(@).find("option:selected")
				cat = selected.val()
				selection.set_selection dim.id, [cat]
		
		selector.chosen()

	
	for dim in spec.dimensions
		break if dim.id == 'vuosi'
	
	# Add year slider
	
	# "Select latest year by default
	selection.set_selection dim.id, dim.categories[dim.categories.length-1].id

	#labels.append $ "<th>#{get_label dim}</th>"
	selrow = $("#yearslider_container")
	$("""
	<span class="variable_label">#{get_label dim}</span>
	<input type="text" id="yearslider"></input>
	""").appendTo selrow

	years = (parseInt c.id for c in dim.categories)
	slider = selrow.find "#yearslider"
	slider.slider
		min: _.min years
		max: _.max years
		tooltip: 'always'
	
	slider.slider().on 'slide', do (dim) -> (ev) ->
		selection.set_selection dim.id, ev.value
	refresh_slider = ->
		value = _.keys(selection.selection['vuosi'])[0]
		slider.slider 'setValue', parseInt value
	
	
	update_map = _.throttle map.refresh_data, 100
	# Hook up the objects to the selection
	selection.$.on 'selection_changed', (ev, args...) ->
		plot.refresh_data args...
		update_map args...
		refresh_slider()
	
	# And force initial change
	selection.trigger_change()

initial_data_promise.fail (args...) ->
	text = """Tietojen lataaminen epäonnistui."""
	if $.support.cors
		text += """ Tarkista internet-yhteytesi
		ja yritä uudelleen päivittämällä sivu.
		"""
	else
		text += """ Selaimesi ei tue vaadittavia
		teknisiä ominaisuuksia. Sovellus on testattu toimivaksi Chrome- ja Firefox-selaimilla."""
	$.notify text,
		autoHide: false
	console.log "Initial data promise fail", args

polygons_promise.fail (args...) ->
	console.log "Fetching polygons failed", args

spec_promise.fail (args...) ->
	console.log "Fetching stat spec failed", args



# A helper function to decode the "state"
# from an url. Using the hash for IE-compatibility :(
get_url_filtering = ->
	hash = window.location.hash
	state = $.deparam hash[1...]
	# Parse from the URL format to the actually used
	# format.
	for dim, catstr of state
		as_obj = {}
		for category in catstr.split(',')
			as_obj[category] = true
		state[dim] = as_obj
	return state

update_url_filtering = (newopts) ->
	state = {}
	# Convert to a bit nicer list-format
	for dim, categories of newopts
		state[dim] = (cat for cat of categories).join(',')
	
	current = get_url_filtering()
	newstate = _.extend current, state
	window.location.hash = $.param newstate

set_colormap_legend = ($el, range, colormap, opts={}) ->
	$el.empty()
	steps = opts.steps ? 10
	stepsize = (range[1] - range[0])/(steps-1)
	value = range[0]
	
	binheight = opts.binheight ? "10px"
	width = 100/steps + "%"
	
	scale = $("""<ul class="colormap_scale" style="clear: both; padding: 10px;">""")
		.appendTo $el
	$el.append $ """
		<div style="float: left; width: 50%">#{range[0]}</div>
		"""
	$el.append $ """
		<div style="float: right; width: 50%; text-align: right;">#{range[1]}</div>
		"""

	
	binvalues = (range[0] + i*stepsize for i in [0...steps])
	for value in binvalues
		bin = $("""<li>""").appendTo scale
		bin.css
			"background-color": colormap(value)
			width: width
			height: binheight
			display: "block"
			float: "left"
			margin: 0
			padding: 0
		bin.prop "title", value
		value += stepsize
	
</script>

</body>
</html>
