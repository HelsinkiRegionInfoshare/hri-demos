<meta charset="utf-8">
<script type="text/javascript" src="../vendor/coffee-script.js"></script>
<script type="text/javascript" src="../vendor/jquery-1.11.0.js"></script>
<script type="text/javascript" src="../vendor/jquery-deparam.js"></script>
<script type="text/javascript" src="../vendor/underscore.js"></script>

<link rel="stylesheet" href="../vendor/leaflet.css" />
<script src="../vendor/leaflet.js"></script>
<script src="../vendor/proj4.js"></script>
<script src="../vendor/proj4leaflet.js"></script>
<script src="../vendor/leaflet-dvf.js"></script>
<script type="text/coffeescript" src="hel_map.coffee"></script>

<script src="../vendor/d3.v3.min.js"></script>
<script src="../vendor/dimple.v2.1.0.js"></script>

<script type="text/javascript" src="../vendor/spin.js"></script>
<script type="text/javascript" src="../vendor/jquery.spin.js"></script>

<link rel="stylesheet" href="../vendor/jquery.powertip/jquery.powertip.css" />
<script type="text/javascript" src="../vendor/jquery.powertip/jquery.powertip.js"></script>

<script type="text/javascript" src="../vendor/chosen/chosen.jquery.js"></script>
<link rel="stylesheet" href="../vendor/chosen/chosen.css" />

<script type="text/coffeescript" src="statsmap.coffee"></script>
<script type="text/coffeescript" src="blockhist.coffee"></script>

<script type="text/javascript" src="../vendor/jquery.history.js"></script>

<style type="text/css">
#map_colormap_legend .colormap_scale {
	padding: 0; margin: 0;
}

/*@font-face {
    font-family: "Droid Sans";
    src: url(sansation_light.woff);
}*/

body, html {
	margin: 0; padding: 0;
	background-color: #eee;
	font-family: "DroidSans",sans-serif;
	color: #555;
}


#header {
	padding: 0px;
}

#header h1 {
	padding-left: 10px;
	padding-top: 7px;
	font-size: 20px;
	text-shadow: -1px 2px 0px #ccc;
}

#header h1 span {
	font-size: 20px;
}

.var_selector {
	display: block;
	position: relative;
	top: 0; width: 100%; right: 0;
	max-height: 100%;
	margin: 0;
	padding: 0;
	padding-right: 1px;
	padding-left: 10px;
	padding: 5px;
	box-sizing: border-box;
	overflow-x: visible;
	overflow-y: auto;
}

.var_selector > li {
	text-shadow: -1px 1px 0px #ccc;
	display: block;
	width: 100%; height: 70px;
	vertical-align: top;
	margin: 0; padding: 10px;
	margin-bottom: 1px;
	box-sizing: border-box;
	background-color: transparent;
	margin-bottom: 5px;
	margin-top: 5px;
	box-shadow: 0px 1px 2px 1px #ccc inset;
	transition: all 0.3s;
	cursor: pointer;
}

.var_selector > li.selected {
	box-shadow: 0px 1px 2px 1px #ccc;
	background-color: white;

}

#grid_container {
	position: absolute;
	box-sizing: border-box;
}

#grid_container > .box-container {
	position: absolute;
	display: block;
	box-sizing: border-box;
	padding: 10px;
}

#grid_container > .box-container > *:last-child {
	width: 100%;
	height: 100%;
	box-sizing: border-box;
	background-color: #fff;
	padding: 5px;
	box-shadow: 0px 1px 4px 1px #ccc;
}

svg * {
	font-size: 14px !important;
}

#map path {
	transition: fill linear 1.0s;
	transition: stroke linear 0.1s;
}

#histogram > table {
	width: 100%; height: 100%;
}

#histogram .histogram_canvas {
	border-bottom: 1px solid black;
}

#histogram .bin {
	left: 0;
	right: 0;
	background-color: rgba(255, 255, 255, 0.0)
	box-sizing: border-box;
	box-shadow: 0px 0px 0px 1px rgba(255, 255, 255, 0.5) inset;
	cursor: pointer;
	transition: all 1s ease-in-out;
	transition: z-index 0s;
	transition: box-shadow 0s;
}

#histogram .bin:hover {
	box-shadow: 0px 0px 0px 2px rgba(255, 255, 255, 0.5) inset, 0px 0px 2px 4px rgba(255, 255, 255, 0.6);
	z-index: 3;
}

#histogram .selected-bin {
	box-shadow: 0px 0px 1px 2px rgba(0, 0, 0, 0.75), 0px 0px 0px 2px rgba(255, 255, 255, 0.5) inset;
	z-index: 2;
}

#histogram .selected-bin:hover {
	box-shadow: 0px 0px 2px 4px rgba(50, 50, 50, 0.75), 0px 0px 0px 2px rgba(255, 255, 255, 0.5) inset;
	z-index: 3;
}

</style>
<div id="header" style="height: 30px;">
<h1>Helsinki alueittain: <span id="area_label"></span> <span style="display: inline-block; position: absolute; right: 190px;" id="variable_label"></span></h1>
</div>

<div id="sidebar" style="margin: 0; padding: 0; position: absolute; width: 180px; top: 0; right: 0; height: 100%;">
</div>
<div id="grid_container" style="left: 0; right: 180px; bottom: 0; top: 30px;">

<div id="map_container"  class="box-container" style="width: 50%; height: 100%;">
<div id="map"></div>
</div>

<div id="histogram_container" class="box-container" style="width: 50%; height: 50%; top: 0; left: 50%;">
<div id="histogram"></div>
</div>

<div id="timeseries_container" class="box-container" style="width: 50%; height: 50%; top: 50%; left: 50%">
<svg id="timeseries"></svg>
</div>

<script type="text/coffeescript">

geojson_area_id = (feature) -> parseInt feature.properties.tunnus

objectify_spec = (spec) ->
	objectify_list = (list) ->
		for value in list
			if _.isObject(value) and 'id' of value
				list[value.id] = value
			objectify_spec value

	objectify_object = (spec) ->
		for key, value of spec
			objectify_spec value
	
	if _.isArray spec
		objectify_list spec
	if _.isObject spec
		objectify_object spec
	
	return spec

class AluesarjatIndicator
	@_get_area_id = (area_code) ->
		numeric = parseInt area_code[area_code.length-3...]
		if numeric != 0
			return numeric
		else
			return area_code
	
	@New: (args...) ->
		$.getJSON(@get_base_url())
		.then (spec) =>
			objectify_spec spec
			self = new @(spec, args...)

	constructor: (@metadata) ->
		@base_url = @metadata._links.self.href
		@_area_id_map = {}
		@area_filter = (area_id) -> true
		for area in @metadata.dimensions.alue.categories
			@_area_id_map[@constructor._get_area_id area.id] = area

	get_reference_area: => @metadata.dimensions.alue.categories[0].id

	get_timeseries_areas: (areas) => areas.concat(@get_reference_area())

	get_area_label: (area_id) =>
		category = @_area_id_map[area_id]
		if not category.label?
			return category.id

		# Hack++
		parts = category.label.split(' ', 3)
		return parts[parts.length-1]
	
	get_reference_year: =>
		current = "#{new Date().getFullYear()}"
		cats = @metadata.dimensions.vuosi.categories
		for cat in cats
			# TODO: HACK! There's an undefined for some reason
			if not cat
				continue
			if cat.id == current
				return cat
		return cats[cats.length-1]

class AluesarjatFunctionIndicator extends AluesarjatIndicator
	@get_base_url: ->
		return @dataset_url
	
	@get_func: -> @func
	
	get_year_data: =>
		year = @get_reference_year().id
		cls = @constructor
		data_promise = $.getJSON "#{cls.get_base_url()}filter&vuosi=#{year}/group_for_columns?as_values=#{cls.field_id},value"
		data_promise.then (raw_data) =>
			data = []
			func = cls.get_func()
			for grp in raw_data
				area_id = cls._get_area_id(grp.alue)
				continue if not @area_filter area_id
				obj = {}
				field_row = grp[cls.field_id]
				for field_val, i in field_row
					obj[field_val] = parseFloat grp.value[i]
				data.push [area_id, func(obj)]
			
			return data: data
	
	get_timeseries_data: (areas) =>
		areas = (@_area_id_map[a].id for a in areas)
		cls = @constructor
		valcols = "#{cls.field_id},value".split(',')
		data_promise = $.getJSON "#{cls.get_base_url()}filter&alue=#{areas.join ','}/group_for_columns?as_values=#{valcols.join ','}"
		data_promise.then (raw_data) ->
			func = cls.get_func()
			datasets = ([] for area in areas)
			d_idx = {}
			for [area, d] in _.zip(areas, datasets)
				d_idx[area] = d
				
			for grp in raw_data
				field_row = grp[cls.field_id]
				obj = {}
				for field_val, i in field_row
					obj[field_val] = parseFloat grp.value[i]
				
				d_idx[grp.alue].push [parseInt(grp.vuosi), func(obj)]
				d_idx[grp.alue].area = cls._get_area_id grp.alue

			return datasets: datasets

valid_share = (nom, denom) =>
	if denom < 1
		return NaN
	return nom/denom


class AluesarjatSimpleShareIndicator extends AluesarjatFunctionIndicator
	@all_id: 'all'
	@formatter: d3.format(".3p")

	@get_func: -> (obj) =>
		valid_share obj[@subset_id], obj[@all_id]

	@get_base_url: ->
		return "#{@dataset_url}filter&#{@field_id}=#{@all_id},#{@subset_id}/"

available_indicators = []

ind = (cls) -> available_indicators.push cls

ind class extends AluesarjatFunctionIndicator
	@title: "Kouluikäisten (7-16v) osuus"
	@formatter: d3.format(".3p")
	@field_id: "ika"
	@ages: ("#{d3.format("02d")(i)}V" for i in [7..16])
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@func: (obj) =>
		subset = 0
		for age in @ages
			subset += obj[age]
		return valid_share subset, obj['99V']

ind class extends AluesarjatFunctionIndicator
	@title: "Alle kouluikäisten (0-6v) osuus"
	@formatter: d3.format(".3p")
	@field_id: "ika"
	@ages: ("#{d3.format("02d")(i)}V" for i in [0..6])
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@func: (obj) =>
		subset = 0
		for age in @ages
			subset += obj[age]
		return valid_share subset, obj['99V']

###
ind class extends AluesarjatFunctionIndicator
	@title: "Kouluikäisiä"
	
	get_timeseries_areas: (areas) =>
		if areas.length == 0
			return [@get_reference_area()]
		return areas
	
	@formatter: d3.format('d')
	@field_id: "ika"
	@ages: ("#{d3.format("02d")(i)}V" for i in [7..16])
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@func: (obj) =>
		subset = 0
		for age in @ages
			subset += obj[age]
		return subset
###

ind class extends AluesarjatFunctionIndicator
	@title: "Yli 65-vuotiaiden osuus"
	@formatter: d3.format(".3p")
	@field_id: "ika"
	@ages: ("#{d3.format("02d")(i)}V" for i in [65..95] by 5)
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@func: (obj) =>
		subset = 0
		for age in @ages
			subset += obj[age]
		return valid_share subset, obj['99V']

###
ind class extends AluesarjatFunctionIndicator
	@title: "Yli 65-vuotiaita"
	
	get_timeseries_areas: (areas) =>
		if areas.length == 0
			return [@get_reference_area()]
		return areas
	
	@formatter: d3.format('d')
	@field_id: "ika"
	@ages: ("#{d3.format("02d")(i)}V" for i in [65..95] by 5)
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@func: (obj) =>
		subset = 0
		for age in @ages
			subset += obj[age]
		return subset
###

ind class extends AluesarjatFunctionIndicator
	@title: "Asukkaita yhteensä"
	
	get_timeseries_areas: (areas) =>
		if areas.length == 0
			return [@get_reference_area()]
		return areas
	
	@formatter: d3.format('d')
	@field_id: "ika"
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@func: (obj) => obj['99V']


ind class extends AluesarjatSimpleShareIndicator
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01s_hki_vakiluku/filter&ika=99V&aidinkieli=all/"
	@field_id: "sukupuoli"
	@subset_id: "2"
	@title: "Naisten osuus"

ind class extends AluesarjatSimpleShareIndicator
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a03s_hki_vakiluku_aidinkieli/"
	@field_id: "aidinkieli"
	@subset_id: "3"
	@title: "Ulkomaankielisten osuus"

ind class extends AluesarjatSimpleShareIndicator
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01s_hki_asuntokunnat/"
	@field_id: "henkiloluku"
	@subset_id: "1"
	@title: "Yksinasuvien osuus"

ind class extends AluesarjatSimpleShareIndicator
	@dataset_url: "http://dev.hel.fi/stats/resources/aluesarjat_a01hkis_vaestoennuste/"
	@field_id: "ika"
	@subset_id: "00V"
	@all_id: "99V"
	@title: "Syntyvyys"

GEOSERVER_BASE_URL = "http://geoserver.hel.fi/geoserver/"



###
$(statsmap).on "area-mouseover", (area_id, value) ->
	mapel.data 'powertipjq', $("<div>#{value}</div>")
	mapel.powerTip "show"

$(statsmap).on "area-mouseout", ->
	mapel.powerTip "hide"
	# Without this the powertip won't change
	# the contents.
	$.powerTip.hide()
###

# Fetch the area polygons in GeoJSON format using WFS
wfs_url = GEOSERVER_BASE_URL + "wfs/"
request_parameters = {
	# Specify the dataset to use
	typeName: 'hel:osaalue_meri'
	# Standard WFS request parameters
	srsName: 'EPSG:4326'
	service: 'WFS'
	version: '1.1.0'
	request: 'GetFeature'
	outputFormat: 'application/json'
}

request_options =
		data: request_parameters
polygons_promise = $.ajax wfs_url, request_options

autoscale_fix = (axis) ->
	min = Infinity
	max = -Infinity
	for row in axis.chart._getAllData()
		x = row[axis.measure]
		min = Math.min x, min
		max = Math.max x, max
	
	axis.overrideMin = min
	axis.overrideMax = max

class _Controller
	constructor: (polygons) ->
				
		@area_selection = []

		mapstuff = hel_leaflet_map "map"
		@$statsmap_el = $("#map")
		map = mapstuff.map
		@statsmap = new StatsMap map,
			legend_el: "#map_colormap_legend"
		
		
		shaped_areas = {}
		for feature in polygons.features
			shaped_areas[geojson_area_id feature] = true
		@area_filter = (a) -> a of shaped_areas

		@statsmap.set_polygons polygons, geojson_area_id
		

			
		el = d3.select("#timeseries")
		chart = new dimple.chart el, []
		chart.x_axis = chart.addMeasureAxis('x', 'Vuosi')
		chart.x_axis.ticks = 5
		chart.y_axis = chart.addMeasureAxis('y', 'value')
		chart.y_axis.ticks = 5
		chart.setMargins 60, 10, 30, 60
		# Uglyuglyugly, see
		# https://github.com/PMSI-AlignAlytics/dimple/issues/99
		#chart.y_axis._getFormat = => @dataset.formatter
		chart.x_axis._getFormat = => (x) -> x
		chart.addSeries(["Vuosi", "Alue"], dimple.plot.line)
		chart.addLegend("0%", "10px", "100%", null, "right")
		@timeseries = chart
		
		@histogram = new BlockHist $("#histogram")
		
		@$timeseries_el = $("#timeseries_container")
		$(window).resize =>
			@timeseries.draw 0, true
		
		
		
		@container_el = $("#grid_container")
		@container_el.powerTip
			followMouse: true
			manual: true
			closeDelay: 0
			fadeOutTime: 0
			fadeInTime: 0

		
		show_tooltip = (area_id, value) =>
			area = @stats.get_area_label area_id
			@container_el.data 'powertipjq', $(
				"""
				<dl>
				<dt>#{area}</dt>
				<dd>#{@stats.constructor.formatter value}</dd>
				</dl>
				"""
				)
			@container_el.powerTip "show"
		
		hide_tooltip = =>
			@container_el.powerTip "hide"; $.powerTip.hide()
		
		$(@statsmap).on "area-mouseover", (e, area_id, value) =>
			show_tooltip area_id, value

		$(@statsmap).on "area-mouseout", ->
			hide_tooltip()

		@histogram.el.on "mouseover", ".bin", (e) =>
			el = $ e.target
			show_tooltip el.data("bin-label"), el.data("bin-value")

		@histogram.el.on "mouseout", ".bin", ->
			hide_tooltip()
		

	set_dataset: (@dataset) =>
		
		@dataset.New().then @_new_stats

	_new_stats: (@stats) =>
		@stats.area_filter = @area_filter
		@area_id_map = {}
		
		$.when(@_update_area_stats(), @_update_timeseries()).then =>
			@statsmap.render()
			# TODO: Ugly hacks
			@histogram.colormap = @statsmap.colormap
			@histogram.formatter = @stats.constructor.formatter
			for area in @area_selection
				@histogram.el.find("[data-bin-label='#{area}']").addClass("selected-bin")
			@histogram.draw()
	
	set_area_selection: (selection) =>
		@area_selection = selection
		@statsmap.set_selection @area_selection
		
		@histogram.el.find(".bin").removeClass("selected-bin")
		for area in selection
			@histogram.el.find("[data-bin-label='#{area}']").addClass("selected-bin")
		@_update_timeseries()

	_update_area_stats: =>
		$("#map_container, #histogram_container").spin()
		p = @stats.get_year_data()
		p.then (data) =>
			@statsmap.set_data data.data, @dataset.formatter
			[areas, values] = _.zip data.data...
			@histogram.set_data values, areas
			$("#map_container, #histogram_container").spin(false)
			
	
	_update_timeseries: =>
		@$timeseries_el.spin()
		areas = @stats.get_timeseries_areas(@area_selection)
		$("#area_label").text @stats.get_area_label areas[0]
		p = @stats.get_timeseries_data areas

		is_invalid = (x) -> not x? or isNaN x
		p.then (data) =>
			entries = []
			for rows in data.datasets
				for [x, y] in rows
					entry = {}
					continue if is_invalid(x) or is_invalid(y)
					entry["Vuosi"] = x
					# Abstraction leakedileaks
					entry["Alue"] = @stats.get_area_label rows.area
					entry[@stats.constructor.title] = y
					entries.push entry
			
			@timeseries.y_axis.measure = @stats.constructor.title
			@timeseries.data = entries
			autoscale_fix @timeseries.x_axis
			autoscale_fix @timeseries.y_axis
			@$timeseries_el.spin(false)
			@timeseries.draw(300)
		

setup = (polygons) ->
	ctrl = new _Controller(polygons)
	
	get_current_state = ->
		$.deparam window.location.search[1..]
	
	prev_dataset = null
	prev_areas = null
	set_state_only = (state) ->
		dataset = available_indicators[state.dataset ? 0]
		areas = state.areas ? []
		
		areas = areas.map parseInt

		dropdown.find('li').removeClass('selected')
		# OMG! Use some kind of id!
		i = available_indicators.indexOf(dataset)
		$(dropdown.find('li')[i]).addClass('selected')
		$("#variable_label").text(dataset.title)

		$.when do ->
			if dataset != prev_dataset
				prev_dataset = dataset
				return ctrl.set_dataset(dataset)
			else
				return null
		.then ->
			if JSON.stringify(areas) != JSON.stringify(prev_areas)
				prev_areas = areas
				ctrl.set_area_selection(areas)
	
	set_state = (state) ->
		History.replaceState null, null, "?" + $.param state
		set_state_only state
	
	
	update_state = (newstuff) ->
		state = $.extend get_current_state(), newstuff
		set_state state

	History.Adapter.bind window, "statechange", ->
		set_state_only get_current_state()
	
	dropdown = $("<ul class='var_selector'>").appendTo $("#sidebar")
	for ind, i in available_indicators
		dropdown.append $ """
		<li data-value="#{i}">#{ind.title}</option>
		"""

	dropdown.on "click", "li", (e) ->
		update_state dataset: $(e.target).data("value")
	
		
	toggle_area_selection = (area_id, value) ->
		return if isNaN value
		if ctrl.area_selection and ctrl.area_selection[0] == area_id
			sel = []
		else
			sel = [area_id]
		update_state areas: sel

	$(ctrl.statsmap).on "area-click", (e, area_id, value) ->
		toggle_area_selection area_id, value
	
	ctrl.histogram.el.on "click", ".bin", (e) ->
		el = $ e.target
		toggle_area_selection el.data("bin-label"), el.data("bin-value")
		


	
	set_state($.deparam window.location.search[1..]).then ->
		ctrl.statsmap.fit_data_bounds()

polygons_promise.then setup
</script>
