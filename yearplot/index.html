<!--
An example and demo for fetching and plotting data from the statistics API
-->

<link href="../vendor/bootstrap/css/bootstrap.min.css" rel="stylesheet">

<!-- Elements that will get populated later on -->
<h1 id="dataset_title"></h1>
<table class="table" id="variable_selectors">
<thead><tr></tr></thead>
<tbody><tr></tr></tbody>
</table>
<div id="lineplot" style="height: 300px"></div>

<!-- Include some handy libraries -->
<script type="text/javascript" src="../vendor/coffee-script.js"></script>
<script type="text/javascript" src="../vendor/jquery-1.11.0.js"></script>
<script type="text/javascript" src="../vendor/underscore.js"></script>
<script type="text/javascript" src="../vendor/bootstrap/js/bootstrap.js"></script>
<script type="text/javascript" src="../vendor/bootstrap-multiselect.js"></script>
<script type="text/javascript" src="../vendor/jquery.flot.js"></script>

<!-- Include the Statproxy class -->
<script type="text/coffeescript" src="http://dev.hel.fi/stats/browser/statproxy.coffee"></script>
<script type="text/coffeescript">
# URL to the JSON-"entrypoint" of the statistic.
# Any resource with a 'vuosi' field could be used here
STAT_RESOURCE="http://dev.hel.fi/stats/resources/aluesarjat_a01hki_asuntokuntien_tulot/"
# Specify columns we use on the axis
XAXIS = 'vuosi'
YAXIS = 'value'

# Get a "proxy object" to the resource
stats = new Statproxy STAT_RESOURCE

# Get the "specification" of the data. Includes
# metadata such as the title and dimensions/categories
# in the dataset.
spec_promise = stats.specification()

# This will be populated later with the dropdowns
category_selection = {}

refresh_data = ->
	# Get a subset of the statistics based
	# on the defined filtering.
	substats = stats.filter category_selection
	# Get the needed data and pass it to the plotter
	substats.columns().done refresh_plot

refresh_plot = (data) ->
	# Convert to numbers
	x = data[XAXIS].map parseFloat
	y = data[YAXIS].map parseFloat
	# Convert the data to "row-format"
	xy = _.zip x, y
	# And plot it
	$.plot "#lineplot", [xy],
		series:
			lines: show: true
			points: show: true


# Set up the selection UI when we get the
# specification
spec_promise.done (spec) ->
	data_specification = spec
	# Set the title
	$("#dataset_title").html spec.metadata.label

	# Get "free" dimensions, eg on which we have
	# to select something
	free_dims = (dim for dim in spec.dimensions \
		when dim.id not in [XAXIS, YAXIS])
	
	# Helper function to get a label of a
	# dimension/category if it exists
	get_label = (obj) ->
		if 'label' of obj
			return obj.label
		return obj.id

	# Set up select boxes for the free variables
	varsel_el = $ "#variable_selectors"
	labels = varsel_el.find("thead tr")
	selectors = varsel_el.find("tbody tr")
	for dim in free_dims
		labels.append $ "<th>#{get_label dim}</th>"
		selrow = $("<td></td>").appendTo selectors
		selector = $("""
		<select name="#{dim.id}">
		</select>
		""").appendTo selrow

		for cat in dim.categories
			selector.append $ """
			<option value="#{cat.id}">
			#{get_label cat}
			</option>
			"""
		# The first option will be selected by default
		category_selection[dim.id] = dim.categories[0].id
		selector.multiselect
			# Update data/plot when selection changes
			# The do-weirness to capture current dim
			onChange: do (dim) -> (el, enabled) ->
				return if not enabled
				category_selection[dim.id] = el.val()
				refresh_data()
				
	
	# Plot the initial data
	refresh_data()


	
</script>
